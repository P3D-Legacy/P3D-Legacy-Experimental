version=2
#0: brain battle bool
:if:<$0>=true
    @text.show(Next up, ...~...~...*The Factory Brain wants~to battle you. Ready?)
:else
    @text.show(Next up, opponent~no.<math.int(<storage.get(factory_streak)>+1)>. Ready?)
:endif
@options.show(Yes,No)
:select:<options.result>
    :when:No
        @text.show(Do you really~want to quit?)
        @options.show(Yes,No)
        :select:<options.result>
            :when:Yes
                @script.start(frontier\battlefactory\challenge\cancelbattle)
        :endselect
:endselect

@text.show(This is your team.~Choose the Pok√©mon you~want to enter the battle with:)
@pokemon.select(1)

:if:<pokemon.selected>>-1
    @pokemon.add(0,<pokemon.data(<pokemon.selected>)>)
    @pokemon.remove(<pokemon.selected>+1)
:endif

@player.turnto(0)
@player.move(1)
@player.turnto(1)
@player.move(1)
@player.turnto(0)
@player.move(1)
@player.warp(frontier\battlefactory\arena.dat,6,0.1,10)
@level.update

@player.move(3)
@player.turnto(1)
@player.move(3)
@player.turnto(0)
@player.move(2)
@player.turnto(3)

@camera.activatethirdperson
@camera.set(0.75,0.745,2.75,-0.885,-0.3)

@script.start(frontier\battlefactory\challenge\choosetrainer,<$0>)
:if:<battle.won>=true
    :if:<$0>=true
        @script.start(frontier\battlefactory\challenge\wonbrain)
    :else
        @storage.set(factory_streak,<storage.get(factory_streak)>+1)
        @script.start(frontier\battlefactory\challenge\afterbattle)
    :endif
:else
    @script.start(frontier\battlefactory\challenge\lostbattle)
:endif
:end